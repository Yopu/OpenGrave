buildscript {
    ext.kotlin_version = '1.2.41'

    repositories {
        jcenter()
        mavenCentral()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'kotlin'
apply plugin: 'com.github.johnrengelman.shadow'
//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.

version = '1.12.2-3.0'
group = 'yopu.opengrave'
archivesBaseName = 'OpenGrave'

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

configurations {
    shadow
    compile.extendsFrom shadow
}

minecraft {
    version = "1.12.2-14.23.3.2655"
    runDir = "run"
    mappings = "snapshot_20180501"
}

repositories {
    mavenCentral()
}

dependencies {
    shadow "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    shadow "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property 'version', project.version
    inputs.property 'mcversion', project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar.dependsOn shadowJar
jar.enabled = false

shadowJar {
    archiveName = tasks.jar.archiveName
    configurations = [project.configurations.shadow]
    relocate 'kotlin', 'opengrave.repack.kotlin'
    relocate 'org.jetbrains', 'opengrave.repack.org.jetbrains'
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}

if (project.hasProperty("testWorldDir")) {
    task cleanSSP(type: Delete) {
        delete fileTree(dir: testWorldDir, include: 'OpenGrave-*.jar')
    }

    task copyToSSP(type: Copy) {
        dependsOn cleanSSP, build
        from tasks.jar.archivePath
        into testWorldDir
    }
}