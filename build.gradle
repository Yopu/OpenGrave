buildscript {
    ext.kotlin_version = '1.0.0-beta-4589'

    repositories {
        jcenter()
        mavenCentral()

        maven {
            name = 'forge'
            url = 'http://files.minecraftforge.net/maven'
        }

        maven {
            name = 'sonatype'
            url = 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
    }

    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.2'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'kotlin'
apply plugin: 'com.github.johnrengelman.shadow'

def mcversion = '1.8.9'
def forgeversion = '11.15.1.1722'

version = "$mcversion-2.1"
group = 'yopu.opengrave'
archivesBaseName = "OpenGrave"

minecraft {
    version = "$mcversion-$forgeversion"
    runDir = 'run'
    mappings = 'snapshot_20160124'
    makeObfSourceJar = false
}

runClient {
    environment 'opengrave.debug', 'true'
}

runServer {
    environment 'opengrave.debug', 'true'
}

repositories {
    mavenCentral()
}

configurations {
    shadow
    compile.extendsFrom shadow
}

dependencies {
    shadow "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    shadow "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
}

processResources {
    exclude('**/*.xcf')

    // this will ensure that this task is redone when the versions change.
    inputs.property 'version', project.version
    inputs.property 'mcversion', project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar.dependsOn shadowJar
jar.enabled = false

shadowJar {
    archiveName = tasks.jar.archiveName
    configurations = [project.configurations.shadow]
    relocate 'kotlin', 'opengrave.repack.kotlin'
    relocate 'org.jetbrains', 'opengrave.repack.org.jetbrains'
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}

task cleanSSP(type: Delete) {
    delete fileTree(dir: testWorldDir, include: 'OpenGrave-*.jar')
}

task copyToSSP(type: Copy, dependsOn: cleanSSP) {
    from tasks.jar.archivePath
    into testWorldDir
}